@echo off
goto start
------
Скрипт перебирает файлы в текущей папке и для каждого обнаруженного JPEG-файла ищет одноименный RAW-файл в папке на 1 уровень выше. Если соответствующий RAW-файл найден, он переносится в папку RAW (создается автоматически).
То есть, если у нас есть папка Photo, в которой потенциально находятся RAW-файлы и есть папка JPEG с отобранными кадрами, нужно скопировать данный скрипт в папку JPEG, а саму папку JPEG поместить в папку Photo, после чего запустить скрипт.

Photo               << здесь будем искать RAW-файлы (название папки не имеет значения)
|_RAW               << сюда перенесутся найденные RAW-файлы (эта папка создается автоматически)
|_JPEG              << здесь отобранные JPEG-файлы (название папки не имеет значения)
  |_raw4jpeg.cmd    << это наш скрипт (название скрипта не имеет значания)
------
v. 1.0 2022-12-07 
------

:start
:: Параметры, которые можно менять:
SET raw_file_ext=ARW& :: расширение RAW-файлов. Например, строка "SET raw_file_ext=ARW" задает расширение .ARW
SET raw_new_folder_name=RAW& :: опеределяет название папки, в которую сприпт переносит найденные RAW-файлы. Например, строка "SET raw_new_folder_name=RAW" задает название папки "RAW"

for %%f in (*.jp*g) do (
	IF EXIST ..\%%~nf.%raw_file_ext% (
		IF NOT EXIST ..\%raw_new_folder_name% md ..\%raw_new_folder_name%
		move /Y ..\%%~nf.%raw_file_ext% ..\%raw_new_folder_name%\%%~nf.%raw_file_ext%
        ) ELSE (
            echo %%~nf.%raw_file_ext% не найден
        )
)

pause